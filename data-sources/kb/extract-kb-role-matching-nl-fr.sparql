PREFIX kb: <http://data.bibliotheken.nl/def#>
SELECT ?manifestation ?isbn ?title ?responsibilityStatement ?description ?author ?authorName ?authorFamilyName ?authorGivenName
WHERE {

  ?manifestation a schema:Book ;
                 schema:inLanguage "fr" ;
                 schema:translationOfWork/schema:inLanguage "nl" .

  OPTIONAL { ?manifestation schema:translationOfWork/rdfs:label ?sourceTitle . }
  OPTIONAL { ?manifestation schema:mainEntityOfPage/kb:ppn ?manifestationID . }

  OPTIONAL { ?manifestation schema:name ?title . }
  OPTIONAL { ?manifestation schema:description ?description . }
  OPTIONAL { ?manifestation rdfs:label ?responsibilityStatement . }
  OPTIONAL { ?manifestation schema:bookEdition ?edition . }
  OPTIONAL { ?manifestation schema:isbn ?isbn . }
  OPTIONAL {
    ?manifestation schema:author ?author . 

    OPTIONAL { ?author schema:name ?authorName . }
    OPTIONAL { ?author schema:familyName ?authorFamilyName . }
    OPTIONAL { ?author schema:givenName ?authorGivenName . }
  }

  #
  # publication needed to filter for year of publication
  #
  OPTIONAL {
    ?manifestation schema:publication ?publication .

    OPTIONAL { ?publication schema:startDate ?publicationYear . }
    OPTIONAL { ?publication schema:organizer/schema:name ?publisherName . }
    OPTIONAL { ?publication schema:location/schema:name ?placeOfPublication . }
  }
 
  FILTER( xsd:integer(str(?publicationYear)) > 1969 && xsd:integer(str(?publicationYear)) < 2021)
}
