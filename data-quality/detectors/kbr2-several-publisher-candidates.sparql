prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix lang: <http://id.loc.gov/vocabulary/languages/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX bnf-onto: <http://data.bnf.fr/ontology/bnf-onto/>
PREFIX frbr: <http://rdvocab.info/uri/schema/FRBRentitiesRDA/>
PREFIX rda-wemi: <http://rdvocab.info/RDARelationshipsWEMI/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/>
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>
PREFIX rdagroup2elements: <http://rdvocab.info/ElementsGr2/>


SELECT DISTINCT
 ?publisherID
 (group_concat(distinct ?publisherLabel;SEPARATOR=';') AS ?publisherLabel)
 (group_concat(distinct ?candidateInfo;SEPARATOR='\n') AS ?candidates)
WHERE {

  #
  # A publisher with match candidate
  #
  graph <http://kbr-linked-authorities> { ?publisher a schema:Organization . }
  graph <http://kbr-publisher-matches> { ?publisher btm:matchCandidate ?candidate . }
  graph <http://kbr-publishers> { ?candidate a schema:Organization . }

  OPTIONAL { graph <http://kbr-linked-authorities> { ?publisher dcterms:identifier ?publisherID . } }
  OPTIONAL { graph <http://kbr-linked-authorities> { ?publisher skos:prefLabel ?publisherLabel . } }
  OPTIONAL { graph <http://kbr-publishers> { ?candidate dcterms:identifier ?candidateID . } }
  OPTIONAL { graph <http://kbr-publishers> { ?candidate skos:prefLabel ?candidateLabel . } }

  #
  # Optional the candidate's address
  #
  OPTIONAL {
    graph <http://kbr-publishers> { ?candidate schema:address ?candidateAddress . }

    OPTIONAL { graph <http://kbr-publishers> { ?candidateAddress schema:addressRegion ?candidateRegion . } }
    OPTIONAL { graph <http://kbr-publishers> { ?candidateAddress schema:addressLocality ?candidateCity . } }
    OPTIONAL { graph <http://kbr-publishers> { ?candidateAddress schema:streetAddress ?candidateStreet . } }
    OPTIONAL { graph <http://kbr-publishers> { ?candidateAddress schema:postalCode ?candidatePostcode . } }
  }


  #
  # Optional the candidate's different language names
  #
  OPTIONAL {
    graph <http://kbr-publishers> { ?candidate schema:name ?candidateNameEN . }
    FILTER( lang(?candidateNameEN) = 'en')
  }

  OPTIONAL {
    graph <http://kbr-publishers> { ?candidate schema:name ?candidateNameNL . }
    FILTER( lang(?candidateNameNL) = 'nl')
  }

  OPTIONAL {
    graph <http://kbr-publishers> { ?candidate schema:name ?candidateNameFR . }
    FILTER( lang(?candidateNameFR) = 'fr')
  }

  BIND(
    CONCAT (
      COALESCE(?candidateID, ""),       " (",
      COALESCE(?candidateLabel, ""),    ", ",
      COALESCE(?candidateNameNL, ""),   ", ",
      COALESCE(?candidateNameFR, ""),   ") ",
      COALESCE(?candidateRegion, ""),   ", ",
      COALESCE(?candidatePostcode, ""), ", ",
      COALESCE(?candidateCity, ""),         ", ",
      COALESCE(?candidateStreet, "")
    ) AS ?candidateInfo )

}
GROUP BY ?publisherID
