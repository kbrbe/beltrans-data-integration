prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix bio: <http://vocab.org/bio/0.1/>
prefix schema: <http://schema.org/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/>
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>
PREFIX rdagroup2elements: <http://rdvocab.info/ElementsGr2/>

  SELECT DISTINCT
    (?manifestation AS ?sourceTextBnFIdentifier)
    (?manifestationTitle AS ?targetTextTitle)
    (?bnfCountryOfOriginLabel AS ?targetTextCountryOfPublication)
    (?bnfDatePublished AS ?targetTextYearOfPublication)
    (?bnfLocationCreated AS ?targetTextPlaceOfPublication)
    (?bnfResponsibilityStatement AS ?targetTextResponsibilityStatement)
    (?bnfSourceLangLabel AS ?sourceTextLanguage)
    (?bnfTargetLangLabel AS ?targetTextLanguage)
    (?isbn13 AS ?targetTextISBN) 
    (?bnfAuthor AS ?authorBnFIdentifier)
    (?bnfAuthorISNIURI AS ?authorISNI)
    (?bnfAuthorNationalityLabel AS ?authorNationality)
    (?bnfAuthorGender AS ?authorGenderBnF)
    (?isniSRUAuthorGenderLabel AS ?authorGenderISNI)
    (?bnfAuthorFamilyName AS ?authorFamilyName)
    (?bnfAuthorGivenName AS ?authorGivenName)
    (?bnfAuthorBirthDate AS ?authorBirthDateBnF)
    (?isniRDFAuthorBirthDate AS ?authorBirthDateISNI)
    (?bnfAuthorDeathDate AS ?authorDeathDateBnF)
    (?isniRDFAuthorDeathDate AS ?authorDeathDateISNI)
    (?bnfTranslator AS ?translatorBnFIdentifier)
    (?bnfTranslatorFamilyName AS ?translatorFamilyName)
    (?bnfTranslatorGivenName AS ?translatorGivenName)
    (?bnfTranslatorISNIURI AS ?translatorISNI)
    (?bnfTranslatorNationalityLabel AS ?translatorNationality)
    (?bnfTranslatorGender AS ?translatorGenderBnF)
    (?isniSRUTranslatorGenderLabel AS ?translatorGenderISNI)
    (?bnfTranslatorBirthDate AS ?translatorBirthDateBnF)
    (?bnfTranslatorDeathDate AS ?translatorDeathDateBnF)
    (?bnfPublisher AS ?publisherBnFIdentifier)
    (?bnfPublisherName AS ?publisherName)
    (?bnfPublisherResourceName AS ?publisherResourceName)
    (?bnfPublisherCountryLabel AS ?publisherCountry)
    (?bnfIllustrator AS ?illustratorBnFIdentifier)
    (?bnfIllustratorFamilyName AS ?illustratorFamilyName)
    (?bnfIllustratorGivenName AS ?illustratorGivenName)
    (?bnfIllustratorISNIURI AS ?illustratorISNI)
    (?bnfIllustratorNationalityLabel AS ?illustratorNationality)
    (?bnfIllustratorGender AS ?illustratorGenderBnF)
    (?bnfScenarist AS ?scenaristBnFIdentifier)
    (?bnfScenaristFamilyName AS ?scenaristFamilyName)
    (?bnfScenaristGivenName AS ?scenaristGivenName)
    (?bnfScenaristISNIURI AS ?scenaristISNI)
    (?bnfScenaristNationalityLabel AS ?scenaristNationality)
    (?bnfScenaristGender AS ?scenaristGenderBnF)

  FROM <http://kbr-syracuse>
  FROM <http://isni-sru>
  FROM <http://kbr-belgians>
  FROM <http://isni-rdf>
  FROM <http://master-data>
  FROM <http://kbr-linked-authorities>
  FROM <http://bnf-publications>
  WHERE {

    graph <http://bnf-publications> { 
      ?manifestation a schema:CreativeWork ;
                     schema:name ?manifestationTitle .
    }
        

    #
    # optional attributes of the KBR translation itself
    #
    OPTIONAL { graph <http://bnf-publications> { ?manifestation schema:datePublished ?bnfDatePublished . }}
    OPTIONAL { graph <http://bnf-publications> { ?manifestation schema:locationCreated ?bnfLocationCreated . }}
    OPTIONAL { graph <http://bnf-publications> { ?manifestation rdfs:comment ?bnfResponsibilityStatement . }}

    OPTIONAL { 
      graph <http://bnf-publications> { ?manifestation schema:inLanguage ?bnfTargetLang . } 

      # within the optional language check optionally for an English label of it (it should exist though)
      OPTIONAL {
        graph <http://master-data> { ?bnfTargetLang mads:authoritativeLabel ?bnfTargetLangLabel . }
        FILTER (lang(?bnfTargetLangLabel) = 'en')
      }
    } 

    #
    # The BnF languages are assigned based on the catalog export, thus if French source is Dutch and if Dutch source is French
    #
    BIND(IF(?bnfTargetLang = 'http://id.loc.gov/vocabulary/languages/fre', iri('http://id.loc.gov/vocabulary/languages/dut'), iri('http://id.loc.gov/vocabulary/languages/fre')) AS ?bnfSourceLang)
    # within the optional language check optionally for an English label of it (it should exist though)
      OPTIONAL {
        graph <http://master-data> { ?bnfSourceLang mads:authoritativeLabel ?bnfSourceLangLabel . }
        FILTER (lang(?bnfSourceLangLabel) = 'en')
      }
    
    OPTIONAL { 
      graph <http://bnf-publications> { ?manifestation schema:countryOfOrigin ?bnfCountryOfOrigin . } 

      OPTIONAL {
        graph <http://master-data> { ?bnfCountryOfOrigin mads:authoritativeLabel ?bnfCountryOfOriginLabel . } 
        FILTER (lang(?bnfCountryOfOriginLabel) = 'en')
      }   
    }   
    
    #
    # Exclude the BnF record if it is found via ISBN in the KBR data (because then we query it already with another query)
    #
    OPTIONAL {
      graph <http://bnf-publications> { ?manifestation bibo:isbn13 ?isbn13 . }  

      #BIND(exists{graph <http://kbr-syracuse> {?kbrManifestation bibo:isbn13 ?isbn13 . } } AS ?kbrLinkExists)
      OPTIONAL {
        graph <http://kbr-syracuse> { ?kbrManifestation bibo:isbn13 ?isbn13 . }
      }
      
    }
    FILTER (!bound(?kbrManifestation))

    # ---------------------------------------------------------------------------
    #
    # (1) AUTHOR
    #
    # if there is an author we optionally check this data source and other data sources for other optional attributes of the author
    #
    OPTIONAL { 
      graph <http://bnf-publications> { ?manifestation schema:author ?bnfAuthor . } 

      OPTIONAL {
        graph <http://bnf-contributors> { 
          ?bnfAuthor a foaf:Person ;
                     rdagroup2elements:countryAssociatedWithThePerson ?bnfAuthorNationality .
        }

        # within this optional nationality check optionally for an English label in the master data
        OPTIONAL {
          graph <http://master-data> { ?bnfAuthorNationality mads:authoritativeLabel ?bnfAuthorNationalityLabel . }
          FILTER (lang(?bnfAuthorNationalityLabel) = 'en')
        }
      }

      # within this optional author, check optional attributes
      OPTIONAL { graph <http://bnf-contributors> { ?bnfAuthor bio:birth ?bnfAuthorBirthDate . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfAuthor bio:death ?bnfAuthorDeathDate . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfAuthor foaf:familyName ?bnfAuthorFamilyName . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfAuthor foaf:givenName ?bnfAuthorGivenName . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfAuthor foaf:gender ?bnfAuthorGender . } }

      # within this optional author, check optionally for authors with ISNI
      OPTIONAL {
        graph <http://bnf-contributors-isni> { ?bnfAuthor skos:exactMatch ?bnfAuthorISNIURI . }

        # if there is an optional ISNI we look it up in Belgians from ISNI SRU
        # optional in case it is not a person marked as Belgian in ISNI SRU
        OPTIONAL {
          graph <http://isni-sru> {
            ?isniSRUAuthor a schema:Person ;
                           bf:identifiedBy ?isniSRUEntity . 

            ?isniSRUEntity a bf:Isni ;
                           rdf:value ?bnfAuthorISNIURI .
          }

          # optional attributes of the ISNI SRU dump
          OPTIONAL { graph <http://isni-sru> { ?isniSRUAuthor schema:gender ?isniSRUAuthorGender . } 

            # within the optional gender check for an optional gender label (it should exist though)
            OPTIONAL {
              graph <http://master-data> { ?isniSRUAuthorGender rdfs:label ?isniSRUAuthorGenderLabel . }
              FILTER (lang(?isniSRUAuthorGenderLabel) = 'en')
            }
          }
        }

        # if there is an optional ISNI we look it up in the ISNI RDF dump
        # since it is a full ISNI dump we should always have a match, but to be sure an "optional"
        OPTIONAL {
          graph <http://isni-rdf> {
            ?isniRDFAuthor a schema:Person ;
                           schema:identifier [ schema:value ?bnfAuthorISNI ] .

          }

          # optional attributes of the ISNI RDF dump
          OPTIONAL { graph <http://isni-rdf> { ?isniRDFAuthor schema:birthDate ?isniRDFAuthorBirthDate . } }
          OPTIONAL { graph <http://isni-rdf> { ?isniRDFAuthor schema:deathDate ?isniRDFAuthorDeathDate . } }
        }
      }


    }

    
    # ---------------------------------------------------------------------------
    #
    # (2) TRANSLATOR
    #
    # if there is a translator we optionally check this data source and other data sources for other optional attributes of the translator
    #
    OPTIONAL { 

      graph <http://bnf-publications> { ?manifestation schema:translator ?bnfTranslator . } 

      OPTIONAL {
        graph <http://bnf-contributors> { 
          # There is always a nationality for this dataset, thus not optional
          ?bnfTranslator a foaf:Person ;
                         rdagroup2elements:countryAssociatedWithThePerson ?bnfTranslatorNationality . 
        }

        # within this optional nationality check optionally for an English label in the master data
        OPTIONAL {
          graph <http://master-data> { ?bnfTranslatorNationality mads:authoritativeLabel ?bnfTranslatorNationalityLabel . }
          FILTER (lang(?bnfTranslatorNationalityLabel) = 'en')
        }
      }


      # within this optional translator, check optional attributes from the linked authorities dataset
      OPTIONAL { graph <http://bnf-contributors> { ?bnfTranslator bio:birth ?bnfTranslatorBirthDate . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfTranslator bio:death ?bnfTranslatorDeathDate . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfTranslator foaf:familyName ?bnfTranslatorFamilyName . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfTranslator foaf:givenName ?bnfTranslatorGivenName . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfTranslator foaf:gender ?bnfTranslatorGender . } }

      # within this optional translator, check optionally for Belgians with ISNI
      OPTIONAL {
        graph <http://bnf-contributors-isni> { ?bnfTranslator skos:exactMatch ?bnfTranslatorISNIURI . }

        # if there is an optional ISNI we look it up in Belgians from ISNI SRU
        # optional in case it is not a person marked as Belgian in ISNI SRU
        OPTIONAL {
          graph <http://isni-sru> {
            ?isniSRUTranslator a schema:Person ;
                           bf:identifiedBy ?isniSRUTranslatorEntity . 

            ?isniSRUTranslatorEntity a bf:Isni ;
                           rdf:value ?bnfTranslatorISNIURI .
          }

          # optional attributes of the ISNI SRU dump
          OPTIONAL { graph <http://isni-sru> { ?isniSRUTranslator schema:gender ?isniSRUTranslatorGender . }

            # within the optional gender check for an optional gender label (it should exist though)
            OPTIONAL {
              graph <http://master-data> { ?isniSRUTranslatorGender rdfs:label ?isniSRUTranslatorGenderLabel . }
              FILTER (lang(?isniSRUTranslatorGenderLabel) = 'en')
            }
          }
        }

        # if there is an optional ISNI we look it up in the ISNI RDF dump
        # since it is a full ISNI dump we should always have a match, but to be sure an "optional"
        OPTIONAL {
          graph <http://isni-rdf> {
            ?isniRDFTranslator a schema:Person ;
                           schema:identifier [ schema:value ?bnfTranslatorISNI ] .

          }

          # optional attributes of the ISNI RDF dump
          OPTIONAL { graph <http://isni-rdf> { ?isniRDFTranslator schema:birthDate ?isniRDFTranslatorBirthDate . } }
          OPTIONAL { graph <http://isni-rdf> { ?isniRDFTranslator schema:deathDate ?isniRDFTraslatorDeathDate . } }
        }
      }


    }
 
    # ---------------------------------------------------------------------------
    #
    # (3) PUBLISHER
    #
    # if there is a publisher we optionally check this data source and other data sources for other optional attributes of the publisher
    #
    OPTIONAL {
      graph <http://bnf-publications> { ?manifestation 	rdagroup1elements:publishersName ?bnfPublisherName . }
    }
    
    # publisher information via publisher resource (not always indicated)
    OPTIONAL { 

      graph <http://bnf-publications> { ?manifestation schema:publisher ?bnfPublisher . } 

      # within this optional publisher, check optional attributes from the linked authorities dataset
      OPTIONAL { 
        
        graph <http://bnf-contributors> { ?bnfPublisher foaf:name ?bnfPublisherResourceName . }
        graph <http://bnf-contributors> { ?bnfPublisher rdagroup2elements:placeAssociatedWithTheCorporateBody ?bnfPublisherCountry . }
        # within this optional country check optionally for an English label in the master data
        OPTIONAL {
          graph <http://master-data> { ?bnfPublisherCountry mads:authoritativeLabel ?bnfPublisherCountryLabel . }
          FILTER (lang(?bnfPublisherCountryLabel) = 'en')
        }
          
      }
    }
    
    
    # ---------------------------------------------------------------------------
    #
    # (4) ILLUSTRATOR
    #
    # if there is a illustrator we optionally check this data source and other data sources for other optional attributes of the illustrator
    #
    OPTIONAL { 

      graph <http://bnf-publications> { ?manifestation marcrel:ill ?bnfIllustrator . } 

      OPTIONAL {
        graph <http://bnf-contributors> { 
          ?bnfIllustrator a foaf:Person ;
                         rdagroup2elements:countryAssociatedWithThePerson ?bnfIllustratorNationality . 
        }

        # within this optional nationality check optionally for an English label in the master data
        OPTIONAL {
          graph <http://master-data> { ?bnfIllustratorNationality mads:authoritativeLabel ?bnfIllustratorNationalityLabel . }
          FILTER (lang(?bnfIllustratorNationalityLabel) = 'en')
        }
      }


      # within this optional translator, check optional attributes from the linked authorities dataset
      OPTIONAL { graph <http://bnf-contributors> { ?bnfIllustrator bio:birth ?bnfIllustratorBirthDate . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfIllustrator bio:death ?bnfIllustratorDeathDate . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfIllustrator foaf:familyName ?bnfIllustratorFamilyName . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfIllustrator foaf:givenName ?bnfIllustratorGivenName . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfIllustrator foaf:gender ?bnfIllustratorGender . } }

      # within this optional translator, check optionally for Belgians with ISNI
      OPTIONAL {
        graph <http://bnf-contributors-isni> { ?bnfIllustrator skos:exactMatch ?bnfIllustratorISNIURI . }

        # if there is an optional ISNI we look it up in Belgians from ISNI SRU
        # optional in case it is not a person marked as Belgian in ISNI SRU
        OPTIONAL {
          graph <http://isni-sru> {
            ?isniSRUIllustrator a schema:Person ;
                           bf:identifiedBy ?isniSRUIllustratorEntity . 

            ?isniSRUTranslatorEntity a bf:Isni ;
                           rdf:value ?bnfIllustratorISNIURI .
          }

          # optional attributes of the ISNI SRU dump
          OPTIONAL { graph <http://isni-sru> { ?isniSRUIllustrator schema:gender ?isniSRUIllustratorGender . }

            # within the optional gender check for an optional gender label (it should exist though)
            OPTIONAL {
              graph <http://master-data> { ?isniSRUIllustratorGender rdfs:label ?isniSRUIllustratorGenderLabel . }
              FILTER (lang(?isniSRUIllustratorGenderLabel) = 'en')
            }
          }
        }

        # if there is an optional ISNI we look it up in the ISNI RDF dump
        # since it is a full ISNI dump we should always have a match, but to be sure an "optional"
        OPTIONAL {
          graph <http://isni-rdf> {
            ?isniRDFIllustrator a schema:Person ;
                           schema:identifier [ schema:value ?bnfIllustratorISNIURI ] .

          }

          # optional attributes of the ISNI RDF dump
          OPTIONAL { graph <http://isni-rdf> { ?isniRDFIllustrator schema:birthDate ?isniRDFIllustratorBirthDate . } }
          OPTIONAL { graph <http://isni-rdf> { ?isniRDFIllustrator schema:deathDate ?isniRDFIllustratorDeathDate . } }
        }
      }

    }
    
    # ---------------------------------------------------------------------------
    #
    # (5) SCENARIST
    #
    # if there is a scenarist we optionally check this data source and other data sources for other optional attributes of the scenarist
    #
    OPTIONAL { 

      graph <http://bnf-publications> { ?manifestation marcrel:sce ?bnfScenarist . } 

      OPTIONAL {
        graph <http://bnf-contributors> { 
          # There is always a nationality for this dataset, thus not optional
          ?bnfScenarist a foaf:Person ;
                         rdagroup2elements:countryAssociatedWithThePerson ?bnfScenaristNationality . 
        }

        # within this optional nationality check optionally for an English label in the master data
        OPTIONAL {
          graph <http://master-data> { ?bnfScenaristNationality mads:authoritativeLabel ?bnfScenaristNationalityLabel . }
          FILTER (lang(?bnfScenaristNationalityLabel) = 'en')
        }
      }


      # within this optional translator, check optional attributes from the linked authorities dataset
      OPTIONAL { graph <http://bnf-contributors> { ?bnfScenarist bio:birth ?bnfScenaristBirthDate . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfScenarist bio:death ?bnfScenaristDeathDate . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfScenarist foaf:familyName ?bnfScenaristFamilyName . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfScenarist foaf:givenName ?bnfScenaristGivenName . } }
      OPTIONAL { graph <http://bnf-contributors> { ?bnfScenarist foaf:gender ?bnfScenaristGender . } }

      # within this optional translator, check optionally for Belgians with ISNI
      OPTIONAL {
        graph <http://bnf-contributors-isni> { ?bnfScenarist skos:exactMatch ?bnfScenaristISNIURI . }

        # if there is an optional ISNI we look it up in Belgians from ISNI SRU
        # optional in case it is not a person marked as Belgian in ISNI SRU
        OPTIONAL {
          graph <http://isni-sru> {
            ?isniSRUScenarist a schema:Person ;
                           bf:identifiedBy ?isniSRUScenaristEntity . 

            ?isniSRUScenaristEntity a bf:Isni ;
                           rdf:value ?bnfScenaristISNIURI .
          }

          # optional attributes of the ISNI SRU dump
          OPTIONAL { graph <http://isni-sru> { ?isniSRUIScenarist schema:gender ?isniSRUIScenaristGender . }

            # within the optional gender check for an optional gender label (it should exist though)
            OPTIONAL {
              graph <http://master-data> { ?isniSRUScenaristGender rdfs:label ?isniSRUScenaristGenderLabel . }
              FILTER (lang(?isniSRUScenaristGenderLabel) = 'en')
            }
          }
        }

        # if there is an optional ISNI we look it up in the ISNI RDF dump
        # since it is a full ISNI dump we should always have a match, but to be sure an "optional"
        OPTIONAL {
          graph <http://isni-rdf> {
            ?isniRDFScenarist a schema:Person ;
                           schema:identifier [ schema:value ?bnfScenaristISNIURI ] .

          }

          # optional attributes of the ISNI RDF dump
          OPTIONAL { graph <http://isni-rdf> { ?isniRDFScenarist schema:birthDate ?isniRDFScenaristBirthDate . } }
          OPTIONAL { graph <http://isni-rdf> { ?isniRDFScenarist schema:deathDate ?isniRDFScenaristDeathDate . } }
        }
      }

    }

}

