prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix lang: <http://id.loc.gov/vocabulary/languages/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX bnf-onto: <http://data.bnf.fr/ontology/bnf-onto/>
PREFIX frbr: <http://rdvocab.info/uri/schema/FRBRentitiesRDA/>
PREFIX rda-wemi: <http://rdvocab.info/RDARelationshipsWEMI/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/>
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>


INSERT {

  graph <http://bnf-publications> {

    ?manifestation a schema:CreativeWork ;
                  schema:name ?manifestationTitle ;
                  schema:datePublished ?manifestationPublishingDate ;
                  bibo:isbn10 ?isbn10 ;
                  bibo:isbn13 ?isbn13 ;
                  schema:author ?author ;
                  schema:locationCreated ?manifestationLocation ;
                  schema:inLanguage lang:fre ;
                  schema:publisher ?publisher ;
                  rdfs:comment ?note ;
                  schema:translator ?translator .
  }
}
WHERE {

    graph <http://bnf-fr> { 
      ?manifestation a frbr:Manifestation ;
                     rda-wemi:expressionManifested ?expression .
    }   
                      
    OPTIONAL { graph <http://bnf-fr> { ?manifestation dcterms:title ?manifestationTitle . } } 
    OPTIONAL { graph <http://bnf-fr> { ?manifestation dcterms:date ?manifestationPublishingDate . } } 
    OPTIONAL { graph <http://bnf-fr> { ?manifestation bibo:isbn10 ?isbn10 . } } 
    OPTIONAL { graph <http://bnf-fr> { ?manifestation bibo:isbn13 ?isbn13 . } } 
    OPTIONAL { graph <http://bnf-fr> { ?manifestation rdagroup1elements:note ?note . } } 

    OPTIONAL { graph <http://bnf-trl-contributor-links> { ?expression marcrel:aut ?author . } } 
    OPTIONAL { graph <http://bnf-trl-contributor-links> { ?expression marcrel:trl ?translator . } } 
    OPTIONAL { graph <http://bnf-trl-contributor-links> { ?expression marcrel:ill ?illustrator . } } 
    OPTIONAL { graph <http://bnf-trl-contributor-links> { ?expression marcrel:sce ?scenarist . } } 
    OPTIONAL { graph <http://bnf-trl-contributor-links> { ?expression rdagroup1elements:publishersName ?publisher . } } 
         
}
