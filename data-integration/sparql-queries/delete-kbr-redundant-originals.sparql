prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix prov: <http://www.w3.org/ns/prov#>
prefix btm: <http://kbr.be/ns/beltrans/model#>

DELETE {
  graph <http://kbr-originals> { ?limitedOriginal ?p ?o . }
  graph <http://kbr-syracuse> { ?m schema:translationOfWork ?limitedOriginal . }
  graph <http://kbr-syracuse> { ?translationActivity prov:used ?limitedOriginal . }
  graph <http://kbr-syracuse> { ?translationActivity rdf:type btm:IdentifiedOriginalActivityExactMatch . }
  graph <http://kbr-syracuse> { ?translationActivity rdf:type btm:IdentifiedOriginalActivitySimilarityMatch . }
}
WHERE {
  graph <http://kbr-syracuse> { 
    ?m a schema:CreativeWork ;
       schema:translationOfWork ?limitedOriginal ;
       schema:translationOfWork ?original . 
  }
        
  graph <http://kbr-originals> { ?original a schema:CreativeWork ; dcterms:identifier ?originalIdentifier. }
  graph <http://kbr-originals> { 
    ?limitedOriginal a schema:CreativeWork ; 
                     dcterms:identifier ?limitedOriginalIdentifier ; 
                     ?p ?o . 
  }
  FILTER(STRSTARTS(?limitedOriginalIdentifier, "source"))
  FILTER(!STRSTARTS(?originalIdentifier, "source"))

  OPTIONAL {
    graph <http://kbr-syracuse> {
      ?translationActivity a prov:Activity ;
                           prov:used ?limitedOriginal .
    }
  }
}
