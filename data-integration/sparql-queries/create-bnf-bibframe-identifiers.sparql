prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix lang: <http://id.loc.gov/vocabulary/languages/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX bnf-onto: <http://data.bnf.fr/ontology/bnf-onto/>
PREFIX frbr: <http://rdvocab.info/uri/schema/FRBRentitiesRDA/>
PREFIX rda-wemi: <http://rdvocab.info/RDARelationshipsWEMI/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/>
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>


INSERT {
  graph <http://bnf-publications> { 
    ?publication bf:identifiedBy ?isbn10Entity ;
                 bf:identifiedBy ?isbn13Entity ;
                 bf:identifiedBy ?bnfEntity .

    ?isbn10Entity a bf:Identifier ;
                  rdfs:label "ISBN-10" ;
                  rdf:value ?isbn10 .

    ?isbn13Entity a bf:Identifier ;
                  rdfs:label "ISBN-13" ;
                  rdf:value ?isbn13 .

    ?bnfEntity a bf:Identifier ;
               rdfs:label "BnF" ;
               rdf:value ?bnfIdentifier .
  }
}
WHERE {
  graph <http://bnf-publications> { ?publication a schema:CreativeWork . }             

  OPTIONAL { graph <http://bnf-publications> { ?publication dcterms:identifier ?bnfIdentifier . } }
  OPTIONAL { graph <http://bnf-publications> { ?publication bibo:isbn10 ?isbn10 . } }
  OPTIONAL { graph <http://bnf-publications> { ?publication bibo:isbn13 ?isbn13 . } }

  BIND(iri(concat("http://kbr.be/id/data/identifier_isbn10_", ?isbn10)) as ?isbn10Entity)
  BIND(iri(concat("http://kbr.be/id/data/identifier_isbn13_", ?isbn13)) as ?isbn13Entity)
  BIND(iri(concat("http://kbr.be/id/data/identifier_bnf_", ?bnfIdentifier)) as ?bnfEntity)
}
