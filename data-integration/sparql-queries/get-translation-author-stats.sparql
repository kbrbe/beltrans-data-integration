# This query returns some simple stats to gain insights, it should be executed against the integrated model
prefix schema: <http://schema.org/>

SELECT
  ?worksWithAuthorCount
  ?worksTotal
  (( ?worksTotal - ?worksWithAuthorCount) AS ?worksWithoutAuthor)
  (( ?worksWithAuthorCount * 100  / ?worksTotal) AS ?percentage)
FROM <http://kbr-syracuse>
WHERE {
  
  # Get the number of works with at least one author
  {
    SELECT (COUNT(distinct ?s) AS ?worksWithAuthorCount)
    WHERE {
      graph <http://kbr-syracuse> {
        ?s a schema:CreativeWork ;
           schema:author ?author .
      }
    }
  }

  # This sub query gets the total number of works
  {
    SELECT (COUNT(distinct ?work) AS ?worksTotal)
    WHERE {
      graph <http://kbr-syracuse> { ?work a schema:CreativeWork . }
    }
  }
}

