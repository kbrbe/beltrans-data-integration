prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/> 
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>
PREFIX rdagroup2elements: <http://rdvocab.info/ElementsGr2/>


SELECT DISTINCT 
  ?sourceLangLabel
  ?targetLangLabel
  ?familyName
  ?givenName
  ?kbrID
WHERE {

  graph <http://beltrans-manifestations> { ?manifestation a schema:CreativeWork . }

  # TARGET LANGUAGE
  #
  OPTIONAL {
    graph <http://beltrans-manifestations> { ?manifestation schema:inLanguage ?targetLang . } 
    OPTIONAL {
      graph <http://master-data> { ?targetLang mads:authoritativeLabel ?targetLangLabel . }
      FILTER (lang(?targetLangLabel) = 'en')
    }
  }

  # SOURCE LANGUAGE
  #
  OPTIONAL {
    graph <http://beltrans-manifestations> { ?manifestation btm:sourceLanguage ?sourceLang . } 
    OPTIONAL {
      graph <http://master-data> { ?sourceLang mads:authoritativeLabel ?sourceLangLabel . }
      FILTER (lang(?sourceLangLabel) = 'en')
    }
  }

  # #########################################################################
  #
  # CONTRIBUTOR
  #
  graph <http://beltrans-manifestations> { ?manifestation schema:author|marcrel:ill|marcrel:sce|marcrel:pbd ?contributor . }
  OPTIONAL { graph <http://beltrans-contributors> { ?contributor rdfs:label ?contributorLabel . } }
  OPTIONAL {
    graph <http://beltrans-contributors> { ?contributor schema:nationality ?contributorNationality . } 
    graph <http://master-data> { ?contributorNationality mads:authoritativeLabel ?contributorNationalityLabel . }
    FILTER (lang(?contributorNationalityLabel) = 'en')
    FILTER( ?contributorNationalityLabel = "Belgium")
  }

  graph <http://beltrans-contributors> { ?contributor schema:sameAs ?kbrContributor . }
  graph <http://kbr-linked-authorities> {
    ?kbrContributor a schema:Person ;
                    dcterms:identifier ?kbrID .
  }
  OPTIONAL { graph <http://kbr-linked-authorities> { ?kbrContributor schema:familyName ?familyName . } }
  OPTIONAL { graph <http://kbr-linked-authorities> { ?kbrContributor schema:givenName ?givenName . } }

}
ORDER BY ?kbrID
