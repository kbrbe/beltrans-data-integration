prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
PREFIX btid: <http://kbr.be/id/data/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/>
prefix btm: <http://kbr.be/ns/beltrans/model#>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>

SELECT 
?contributorUnescoID
?contributorUnescoIDLong
?contributorBELTRANSID
?contributorName 
(GROUP_CONCAT(DISTINCT ?manifestationTitle;SEPARATOR=";") AS ?titles)
WHERE {
  
  #
  # It should be Unesco contributors
  #
  graph <http://unesco> { 
    ?manifestation schema:name ?manifestationTitle ;
                   schema:author|schema:translator|marcrel:ill|marcrel:sce|marcrel:pbd ?unescoContributor .
  }
        
  {
  SELECT ?unescoContributor ?contributorUnescoID ?contributorUnescoIDLong ?contributorBELTRANSID ?contributorName
  WHERE {
    #   
    # We are only interested in person contributors that do have an Unesco identifier
    #   
    graph <http://unesco-linked-authorities> {
      ?unescoContributor a schema:Person ;
                         rdfs:label ?contributorName ;
                         dcterms:identifier ?contributorUnescoID ;
                         bf:identifiedBy ?unescoContributorLongIDEntity .

      ?unescoContributorLongIDEntity a bf:Identifier ;
                                     rdfs:label "UnescoLong" ;
                                     rdf:value ?contributorUnescoIDLong .
    }   

    graph <http://beltrans-contributors> {
      ?contributor schema:sameAs ?unescoContributor ;
                   dcterms:identifier ?contributorBELTRANSID .
    }
               
    FILTER NOT EXISTS {
      graph <http://beltrans-contributors> { ?contributor bf:identifiedBy/rdfs:label ?existingIdentifier . }
      FILTER(?existingIdentifier IN ("ISNI", "VIAF", "Wikidata", "KBR", "BnF", "NTA") )
    }   
  }
  }
        
}
GROUP BY ?contributorUnescoID ?contributorUnescoIDLong ?contributorBELTRANSID ?contributorName

