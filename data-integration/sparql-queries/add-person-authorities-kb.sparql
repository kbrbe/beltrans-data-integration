prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix lang: <http://id.loc.gov/vocabulary/languages/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX bnf-onto: <http://data.bnf.fr/ontology/bnf-onto/>
PREFIX frbr: <http://rdvocab.info/uri/schema/FRBRentitiesRDA/>
PREFIX rda-wemi: <http://rdvocab.info/RDARelationshipsWEMI/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/>
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>


#SELECT ?authorityURI ?uuid ?authorityURIISNI ?kbAuthorityISNI
INSERT {
  graph <http://beltrans-contributors> {
    ?authorityURI a schema:Person ;
      rdfs:label ?authorityLabel ;
      schema:nationality ?kbAuthorityNationality ;
      schema:familyName ?kbAuthorityFamilyName ;
      schema:givenName ?kbAuthorityGivenName ;
      schema:sameAs ?kbAuthority ;
      rdfs:comment "Created from KB data" ;
      bf:identifiedBy ?authorityURIISNI ;
      bf:identifiedBy ?viafEntityURI ;
      bf:identifiedBy ?wdEntityURI .

    ?authorityURIISNI a bf:Isni ;
      rdf:value ?kbAuthorityISNI .

    ?viafEntityURI a bf:Identifier ;
                   rdfs:label "VIAF" ;
                   rdf:value ?viaf .

    ?wdEntityURI a bf:Identifier ;
                 rdfs:label "Wikidata" ;
                 rdf:value ?wd .



  }
}
WHERE {

    graph <http://kb-linked-authorities> { ?kbAuthority a schema:Person . }   

    #
    # Get some optional properties
    #
    OPTIONAL { graph <http://kb-linked-authorities> { ?kbAuthority schema:familyName ?kbAuthorityFamilyName . } }
    OPTIONAL { graph <http://kb-linked-authorities> { ?kbAuthority schema:givenName ?kbAuthorityGivenName . } }
    OPTIONAL { graph <http://kb-linked-authorities> { ?kbAuthority schema:nationality ?kbAuthorityNationality . } }

    BIND( STRUUID() as ?uuid)
    BIND( IRI( CONCAT( "http://kbr.be/id/data/", ?uuid ) ) as ?authorityURI)
    BIND( CONCAT( "BELTRANS authority ", ?uuid ) as ?authorityLabel)

    #
    # Get optional VIAF
    #
    OPTIONAL {
      graph <http://kb-linked-authorities> {
        ?kbAuthority bf:identifiedBy ?viafEntity .

        ?viafEntity a bf:Identifier ;
                    rdfs:label "VIAF" ;
                    rdf:value ?viaf .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_viaf_", ?viaf)) as ?viafEntityURI)
    }

    #
    # Get optional WIKIDATA
    #
    OPTIONAL {
      graph <http://kb-linked-authorities> {
        ?kbAuthority bf:identifiedBy ?wdEntity .

        ?wdEntity a bf:Identifier ;
                  rdfs:label "Wikidata" ;
                  rdf:value ?wd .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_wikidata_", ?wd)) as ?wdEntityURI)
    }


    #
    # Get the linked ISNI identifier
    #
    OPTIONAL {
      graph <http://kb-linked-authorities> { 
        ?kbAuthority bf:identifiedBy ?isniAuthorEntity . 
        ?isniAuthorEntity a bf:Isni ;
                    rdf:value ?kbAuthorityISNI . 
      }
      BIND( IRI( CONCAT( "http://kbr.be/id/data/identifier_isni_", ?kbAuthorityISNI ) ) as ?authorityURIISNI)
    }         

    #
    # Only create a new authority if there isn't already an authority with the given ISNI
    #
    FILTER NOT EXISTS {
      graph <http://beltrans-contributors> {
        ?p a schema:Person ;
           schema:sameAs ?kbAuthority .
      }
    }
}
