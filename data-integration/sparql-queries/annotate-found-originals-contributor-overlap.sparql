prefix skos: <http://www.w3.org/2004/02/skos/core#>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix up: <http://users.ugent.be/~tdenies/up/>
prefix prov: <http://www.w3.org/ns/prov#>
prefix btid: <http://kbr.be/id/data/>
prefix btm: <http://kbr.be/ns/beltrans/model#>

INSERT {
  graph <http://kbr-syracuse> { ?translationActivity btm:verifiedBy btid:overlappingContributors . }
}
WHERE {
  
  graph <http://kbr-syracuse> {
    ?translationActivity a prov:Activity ;
                         up:assertionConfidence ?similarity ;
                         prov:generated ?m ;
                         prov:used ?original .
    
    ?m a schema:CreativeWork ;
       dcterms:identifier ?translationID .
  }
        
  # Uncomment the following if all manifestations should be checked
  # otherwise only the BELTRANS manifestations are checked
  #graph <http://beltrans-manifestations> { 
  #  ?integrated schema:sameAs ?m ;
  #              schema:isPartOf btid:beltransCorpus .
  #}
        
  graph <http://kbr-originals> {
    ?original a schema:CreativeWork ;
              dcterms:identifier ?originalID .
  }
  FILTER(!STRSTARTS(?originalID, "source"))
        
  #
  # There has to be at least one overlapping contributor between translation and original
  #
  graph <http://kbr-syracuse> { ?m ?contributorRelationship ?contributor . }
  graph <http://kbr-originals> { ?original ?contributorRelationshipOriginal ?contributor . }

  # the overlap has to be because of a contributor not other triples
  #
  graph <http://kbr-linked-authorities> { ?contributor a ?contributorType . }
  FILTER(?contributorType IN (schema:Person, schema:Organization))

}
