prefix schema: <http://schema.org/>
prefix btid: <http://kbr.be/id/data/>
prefix bibo: <http://purl.org/ontology/bibo/>
prefix bf: <http://id.loc.gov/ontologies/bibframe/>
prefix marcrel: <http://id.loc.gov/vocabulary/relators/>
prefix btm: <http://kbr.be/ns/beltrans/model#>

SELECT ?m ?keyPart1 ?keyPart2
WHERE {
  
  graph <http://beltrans-manifestations> {
    ?m a schema:CreativeWork ;
       schema:isPartOf btid:beltransCorpus ;
       schema:name|bibo:isbn10|bibo:isbn13 ?keyPart1 .
  }
      
  {
    
    #
    # ALTERNATIVE 1: Authorized contributor name
    #
    {
      graph <http://beltrans-manifestations> { ?m schema:author|schema:translator|marcrel:ill|marcrel:sce|marcrel:pbd ?contributor . }

      graph <http://beltrans-contributors> { 
        ?contributor a ?contributorType ;
                     rdfs:label ?keyPart2 .
      }
      FILTER( ?contributorType IN (schema:Person, schema:Organization) )
    }
    
    UNION
    
    #
    # ALTERNATIVE 2: Alternative contributor name
    #
    {
      graph <http://beltrans-manifestations> { ?m schema:author|schema:translator|marcrel:ill|marcrel:sce|marcrel:pbd ?contributor . }
            
      graph <http://beltrans-contributors> { 
        ?contributor a schema:Person ;
                     schema:sameAs ?kbrContributor .
      }
      
      graph <http://kbr-syracuse> {
        ?kbrContributor a schema:Person ;
                        btm:hasNameVariant|btm:hasPseudonym|btm:isPseudonymOf ?nameVariant .
        
        ?nameVariant rdfs:label ?keyPart2 .
      }
    }

  }

}

