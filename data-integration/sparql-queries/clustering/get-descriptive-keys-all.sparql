prefix schema: <http://schema.org/>
prefix btid: <http://kbr.be/id/data/>
prefix bf: <http://id.loc.gov/ontologies/bibframe/>
prefix bibo: <http://purl.org/ontology/bibo/>
prefix marcrel: <http://id.loc.gov/vocabulary/relators/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix btm: <http://kbr.be/ns/beltrans/model#>

SELECT ?mID ?keyPart1 ?keyPart2
FROM <http://beltrans-manifestations>
FROM <http://beltrans-originals>
WHERE {
  
  graph <http://beltrans-manifestations> {
    ?m a schema:CreativeWork ;
       dcterms:identifier ?mID ;
       schema:name|bibo:isbn10|bibo:isbn13 ?keyPart1 .
  }

  # schema:name and ISBNs are in the original graph
  # therefore not specifying any graph and using the FROM clause
  #
  ?m schema:name|bibo:isbn10|bibo:isbn13|(schema:translationOfWork/schema:name)|(schema:translationOfWork/bibo:isbn10)|(schema:translationOfWork/bibo:isbn13) ?keyPart1 .
  
  FILTER ( ?keyPart1 != "")
        
  {
    
    #
    # KEY PART 2, ALTERNATIVE 1: Authorized contributor name
    #
    {
      graph <http://beltrans-manifestations> { ?m schema:author|schema:translator|marcrel:ill|marcrel:sce|marcrel:pbd ?contributor . }

      graph <http://beltrans-contributors> { 
        ?contributor a ?contributorType ;
                     rdfs:label ?keyPart2 .
      }
      FILTER( ?contributorType IN (schema:Person, schema:Organization) )
    }
    
    UNION
    
    #
    # KEY PART 2, ALTERNATIVE 2: Alternative contributor name
    #
    {
      graph <http://beltrans-manifestations> { ?m schema:author|schema:translator|marcrel:ill|marcrel:sce|marcrel:pbd ?contributor . }
            
      graph <http://beltrans-contributors> { 
        ?contributor a schema:Person ;
                     schema:sameAs ?kbrContributor .
      }
      
      graph <http://kbr-syracuse> {
        ?kbrContributor a schema:Person ;
                        btm:hasNameVariant|btm:hasPseudonym|btm:isPseudonymOf ?nameVariant .
        
        ?nameVariant rdfs:label ?keyPart2 .
      }
    }
  
  }

  FILTER ( ?keyPart2 != "")
}

