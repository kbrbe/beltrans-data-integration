prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/> 
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>
PREFIX rdagroup2elements: <http://rdvocab.info/ElementsGr2/>

INSERT {
  graph <http://beltrans-manifestations> { 
    ?manifestation schema:isPartOf btid:beltransCorpus ;
                   a btm:BeltransTranslation .
  }
}
WHERE {
  {
  SELECT DISTINCT 
    ?manifestation
    (group_concat(distinct ?authorNationalityLabel;SEPARATOR=';') AS ?authorNationalities)
    (group_concat(distinct ?illustratorNationalityLabel;SEPARATOR=';') AS ?illustratorNationalities)
    (group_concat(distinct ?scenaristNationalityLabel;SEPARATOR=';') AS ?scenaristNationalities)
    (group_concat(distinct ?publishingDirectorNationalityLabel;SEPARATOR=';') AS ?publishingDirectorNationalities)
  WHERE {

    graph <http://beltrans-manifestations> { 
      ?manifestation a schema:CreativeWork .
    }

    # #########################################################################
    #
    # AUTHOR
    #
    OPTIONAL {
      graph <http://beltrans-manifestations> { ?manifestation schema:author ?author . }
      OPTIONAL { graph <http://beltrans-contributors> { ?author rdfs:label ?authorLabel . } }
      OPTIONAL {
        graph <http://beltrans-contributors> { ?author schema:nationality|schema:location/schema:addressCountry ?authorNationality . } 
        graph <http://master-data> { ?authorNationality mads:authoritativeLabel ?authorNationalityLabel . }
        FILTER (lang(?authorNationalityLabel) = 'en')
      }
    }

    # #########################################################################
    #
    # TRANSLATOR
    #
    OPTIONAL {
      graph <http://beltrans-manifestations> { ?manifestation schema:translator ?translator . }
      OPTIONAL { graph <http://beltrans-contributors> { ?translator rdfs:label ?translatorLabel . } }
      OPTIONAL {
        graph <http://beltrans-contributors> { ?translator schema:nationality|schema:location/schema:addressCountry ?translatorNationality . } 
        graph <http://master-data> { ?translatorNationality mads:translatoritativeLabel ?translatorNationalityLabel . }
        FILTER (lang(?translatorNationalityLabel) = 'en')
      }
    }

    # #########################################################################
    #
    # ILLUSTRATOR
    #
    OPTIONAL {
      graph <http://beltrans-manifestations> { ?manifestation marcrel:ill ?illustrator . }
      OPTIONAL {
        graph <http://beltrans-contributors> { ?illustrator schema:nationality|schema:location/schema:addressCountry ?illustratorNationality . } 
        graph <http://master-data> { ?illustratorNationality mads:authoritativeLabel ?illustratorNationalityLabel . }
        FILTER (lang(?illustratorNationalityLabel) = 'en')
      }
      OPTIONAL { graph <http://beltrans-contributors> { ?illustrator rdfs:label ?illustratorLabel . } }
    }

    # #########################################################################
    #
    # SCENARIST
    #
    OPTIONAL {
      graph <http://beltrans-manifestations> { ?manifestation marcrel:sce ?scenarist . }
      OPTIONAL {
        graph <http://beltrans-contributors> { ?scenarist schema:nationality|schema:location/schema:addressCountry ?scenaristNationality . } 
        graph <http://master-data> { ?scenaristNationality mads:authoritativeLabel ?scenaristNationalityLabel . }
        FILTER (lang(?scenaristNationalityLabel) = 'en')
      }
      OPTIONAL { graph <http://beltrans-contributors> { ?scenarist rdfs:label ?scenaristLabel . } }
    }

    # #########################################################################
    #
    # PUBLISHING DIRECTOR
    #
    OPTIONAL {
      graph <http://beltrans-manifestations> { ?manifestation marcrel:pbd ?publishingDirector . }
      OPTIONAL {
        graph <http://beltrans-contributors> { ?publishingDirector schema:nationality|schema:location/schema:addressCountry ?publishingDirectorNationality . } 
        graph <http://master-data> { ?publishingDirectorNationality mads:authoritativeLabel ?publishingDirectorNationalityLabel . }
        FILTER (lang(?publishingDirectorNationalityLabel) = 'en')
      }
      OPTIONAL { graph <http://beltrans-contributors> { ?publishingDirector rdfs:label ?publishingDirectorLabel . } }
    }

  }
  GROUP BY ?manifestation
  HAVING(CONTAINS(str(?authorNationalities), "Belgium") || CONTAINS(str(?illustratorNationalities), "Belgium") || CONTAINS(str(?scenaristNationalities), "Belgium") || CONTAINS(str(?publishingDirectorNationalities), "Belgium"))
  }
}
