PREFIX schema: <http://schema.org/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/> 

INSERT {
  graph <http://beltrans-manifestations> {
    ?manifestation ?contributionRelationship ?contributor .
  }
}
USING <http://kbr-syracuse>
USING <http://kb-publications>
USING <http://bnf-publications>
USING <http://unesco>
WHERE {
  
    # (1) follow sameAs links to get all local manifestations ...
    #
    graph <http://beltrans-manifestations> {
      ?manifestation a schema:CreativeWork ;
                     schema:name ?title ;
                     schema:sameAs ?localManifestation .
    } 

    # (2) ... to get the local contributor ..
    #
    ?localManifestation ?contributionRelationship ?localContributor .
  
    # (3) ... which we follow to get the integrated contributor
    #
    graph <http://beltrans-contributors> { ?contributor schema:sameAs ?localContributor . }  

    # We want to fetch the following contributor relationships
    #
    FILTER(?contributionRelationship IN (schema:author, schema:translator, schema:publisher, marcrel:ill, marcrel:sce, marcrel:pbd))
}
