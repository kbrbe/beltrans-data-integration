prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix lang: <http://id.loc.gov/vocabulary/languages/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX bnf-onto: <http://data.bnf.fr/ontology/bnf-onto/>
PREFIX frbr: <http://rdvocab.info/uri/schema/FRBRentitiesRDA/>
PREFIX rda-wemi: <http://rdvocab.info/RDARelationshipsWEMI/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/>
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>


#SELECT ?authorityURI ?uuid ?authorityURIISNI ?isniAuthorityISNI ?viaf ?nta ?dnb ?wd ?familyName ?givenName
INSERT {
  graph <http://beltrans-contributors> {
    ?authorityURI a schema:Organization ;
      rdfs:label ?isniAuthorityLabel ;
      dcterms:identifier ?uuid ;
      schema:sameAs ?isniAuthority ;
      rdfs:comment "Created from ISNI SRU data" ;
      bf:identifiedBy ?authorityURIISNI ; 
      bf:identifiedBy ?viafEntityURI ;
      bf:identifiedBy ?ntaEntityURI ;
      bf:identifiedBy ?dnbEntityURI ;
      bf:identifiedBy ?wdEntityURI .

    ?authorityURIISNI a bf:Isni ;
      rdf:value ?isniAuthorityISNI .

    ?viafEntityURI a bf:Identifier ;
                   rdfs:value "VIAF" ;
                   rdf:value ?viaf .

    ?ntaEntityURI a bf:Identifier ;
                  rdfs:value "NTA" ;
                  rdf:value ?nta .

    ?dnbEntityURI a bf:Identifier ;
                  rdfs:value "DNB" ;
                  rdf:value ?dnb .

    ?wdEntityURI a bf:Identifier ;
                 rdfs:value "Wikidata" ;
                 rdf:value ?wd .
  }
}
WHERE {

    graph <http://isni-sru> { ?isniAuthority a schema:Organization . }   

    #
    # Get some optional properties
    #
    OPTIONAL { graph <http://isni-sru> { ?isniAuthority rdfs:label ?isniAuthorityLabel . } }

    BIND( STRUUID() as ?uuid)
    BIND( IRI( CONCAT( "http://kbr.be/id/data/", ?uuid ) ) as ?authorityURI)

    #
    # Get optional VIAF
    #
    OPTIONAL {
      graph <http://isni-sru> {
        ?isniAuthority bf:identifiedBy ?viafEntity .

        ?viafEntity a bf:Identifier ;
                    rdfs:label "VIAF" ;
                    rdf:value ?viaf .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_viaf_", ?viaf)) as ?viafEntityURI)
    }

    #
    # Get optional WIKIDATA
    #
    OPTIONAL {
      graph <http://isni-sru> {
        ?isniAuthority owl:sameAs ?wdEntity .

        FILTER( regex(str(?wdEntity), "wikidata", "i"))
      }
      BIND(STRAFTER(str(?wdEntity), "wikidata.org/entity/") as ?wd)
      BIND(iri(concat("http://kbr.be/id/data/identifier_wikidata_", ?wd)) as ?wdEntityURI)
    }

    #
    # Get optional KB (NTA)
    #
    OPTIONAL {
      graph <http://isni-sru> {
        ?isniAuthority bf:identifiedBy ?ntaEntity .

        ?ntaEntity a bf:Identifier ;
                    rdfs:label "NTA" ;
                    rdf:value ?nta .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_nta_", ?nta)) as ?ntaEntityURI)
    }

    #
    # Get optional DNB
    #
    OPTIONAL {
      graph <http://isni-sru> {
        ?isniAuthority bf:identifiedBy ?dnbEntity .

        ?dnbEntity a bf:Identifier ;
                   rdfs:label "DNB" ;
                   rdf:value ?dnb .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_dnb_", ?dnb)) as ?ntaEntityURI)
    }



    #
    # Get the linked ISNI identifier
    #
    OPTIONAL {
      graph <http://isni-sru> { 
        ?isniAuthority bf:identifiedBy ?isniAuthorEntity . 
        ?isniAuthorEntity a bf:Isni ;
                    rdf:value ?isniAuthorityISNI . 
      }
      BIND( IRI( CONCAT( "http://kbr.be/id/data/identifier_isni_", ?isniAuthorityISNI ) ) as ?authorityURIISNI)
    }         

    #
    # Only create a new authority if there isn't already an authority with the given ISNI
    #
    FILTER NOT EXISTS {
      graph <http://beltrans-contributors> {
        ?p a schema:Organization ;
           schema:sameAs ?isniAuthority .
      }
    }
}
