prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix lang: <http://id.loc.gov/vocabulary/languages/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX bnf-onto: <http://data.bnf.fr/ontology/bnf-onto/>
PREFIX frbr: <http://rdvocab.info/uri/schema/FRBRentitiesRDA/>
PREFIX rda-wemi: <http://rdvocab.info/RDARelationshipsWEMI/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/>
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>


#SELECT ?authorityURI ?uuid ?authorityURIISNI ?wikidataAuthorityISNI
INSERT {
  graph <http://beltrans-contributors> {
    ?authorityURI a schema:Person ;
      rdfs:label ?wikidataAuthorityLabel ;
      dcterms:identifier ?uuid ;
      schema:nationality ?wikidataAuthorityNationality ;
      schema:familyName ?wikidataAuthorityFamilyName ;
      schema:givenName ?wikidataAuthorityGivenName ;
      schema:sameAs ?wikidataAuthority ;
      rdfs:comment "Created from Wikidata data" ;
      bf:identifiedBy ?authorityURIISNI ;
      bf:identifiedBy ?viafEntityURI ;
      bf:identifiedBy ?kbrEntityURI ;
      bf:identifiedBy ?bnfEntityURI ;
      bf:identifiedBy ?ntaEntityURI ;
      bf:identifiedBy ?ownIDEntityURI .

    ?authorityURIISNI a bf:Isni ;
                      rdfs:comment "Created from Wikidata data" ;
                      rdf:value ?wikidataAuthorityISNI .

    ?viafEntityURI a bf:Identifier ;
                   rdfs:comment "Created from Wikidata data" ;
                   rdfs:label "VIAF" ;
                   rdf:value ?viaf .

    ?ownIDEntityURI a bf:Identifier ;
                    rdfs:comment "Created from Wikidata data" ;
                    rdfs:label "Wikidata" ;
                    rdf:value ?ownID .

    ?kbrEntityURI a bf:Identifier ;
                  rdfs:comment "Created from Wikidata data" ;
                  rdfs:label "KBR" ;
                  rdf:value ?kbr .

    ?bnfEntityURI a bf:Identifier ;
                  rdfs:comment "Created from Wikidata data" ;
                  rdfs:label "BnF" ;
                  rdf:value ?bnf .

    ?ntaEntityURI a bf:Identifier ;
                 rdfs:comment "Created from Wikidata data" ;
                 rdfs:label "NTA" ;
                 rdf:value ?nta .

  }
}
WHERE {

    graph <http://wikidata> { ?wikidataAuthority a schema:Person . }   

    #
    # Get some optional properties
    #
    OPTIONAL { graph <http://wikidata> { ?wikidataAuthority rdfs:label ?wikidataAuthorityLabel . } }
    OPTIONAL { graph <http://wikidata> { ?wikidataAuthority schema:familyName ?wikidataAuthorityFamilyName . } }
    OPTIONAL { graph <http://wikidata> { ?wikidataAuthority schema:givenName ?wikidataAuthorityGivenName . } }
    OPTIONAL { graph <http://wikidata> { ?wikidataAuthority schema:nationality ?wikidataAuthorityNationality . } }

    BIND( STRUUID() as ?uuid)
    BIND( IRI( CONCAT( "http://kbr.be/id/data/", ?uuid ) ) as ?authorityURI)

    # Get optional own ID
    OPTIONAL {
      graph <http://wikidata> { ?wikidataAuthority dcterms:identifier ?ownID . } 
      BIND(iri(concat("http://kbr.be/id/data/identifier_kbr_", ?ownID)) as ?ownIDEntityURI)
    }

    #
    # Get optional VIAF
    #
    OPTIONAL {
      graph <http://wikidata> {
        ?wikidataAuthority bf:identifiedBy ?viafEntity .

        ?viafEntity a bf:Identifier ;
                    rdfs:label "VIAF" ;
                    rdf:value ?viaf .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_viaf_", ?viaf)) as ?viafEntityURI)
    }

    #
    # Get optional KBR
    #
    OPTIONAL {
      graph <http://wikidata> {
        ?wikidataAuthority bf:identifiedBy ?kbrEntity .

        ?kbrEntity a bf:Identifier ;
                   rdfs:label "KBR" ;
                   rdf:value ?kbr .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_kbr_", ?kbr)) as ?kbrEntityURI)
    }

    #
    # Get optional BnF
    #
    OPTIONAL {
      graph <http://wikidata> {
        ?wikidataAuthority bf:identifiedBy ?bnfEntity .

        ?bnfEntity a bf:Identifier ;
                   rdfs:label "BnF" ;
                   rdf:value ?bnf .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_bnf_", ?bnf)) as ?bnfEntityURI)
    }

    #
    # Get optional KB (NTA)
    #
    OPTIONAL {
      graph <http://wikidata> {
        ?wikidataAuthority bf:identifiedBy ?ntaEntity .

        ?ntaEntity a bf:Identifier ;
                   rdfs:label "NTA" ;
                   rdf:value ?nta .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_nta_", ?nta)) as ?ntaEntityURI)
    }

    #
    # Get the linked ISNI identifier
    #
    OPTIONAL {
      graph <http://wikidata> { 
        ?wikidataAuthority bf:identifiedBy ?isniAuthorEntity . 
        ?isniAuthorEntity a bf:Isni ;
                    rdf:value ?wikidataAuthorityISNI . 
      }
      BIND( IRI( CONCAT( "http://kbr.be/id/data/identifier_isni_", ?wikidataAuthorityISNI ) ) as ?authorityURIISNI)
    }         

    #
    # Only create a new authority if there isn't already an authority with the given ISNI
    #
    FILTER NOT EXISTS {
      graph <http://beltrans-contributors> {
        ?p a schema:Person ;
           schema:sameAs ?wikidataAuthority .
      }
    }
}
