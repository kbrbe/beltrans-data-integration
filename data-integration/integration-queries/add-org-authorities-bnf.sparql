prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix lang: <http://id.loc.gov/vocabulary/languages/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX bnf-onto: <http://data.bnf.fr/ontology/bnf-onto/>
PREFIX frbr: <http://rdvocab.info/uri/schema/FRBRentitiesRDA/>
PREFIX rda-wemi: <http://rdvocab.info/RDARelationshipsWEMI/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/>
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>
PREFIX rdagroup2elements: <http://rdvocab.info/ElementsGr2/>


#SELECT ?authorityURI ?uuid ?authorityURIISNI ?bnfAuthorityISNI
INSERT {
  graph <http://beltrans-contributors> {
    ?authorityURI a schema:Organization ;
      rdfs:label ?bnfAuthorityLabel ;
      dcterms:identifier ?uuid ;
      schema:sameAs ?bnfAuthority ;
      schema:location ?countryEntityURI ;
      rdfs:comment "Created from BnF data" ;
      bf:identifiedBy ?authorityURIISNI ;
      bf:identifiedBy ?viafEntityURI ;
      bf:identifiedBy ?wdEntityURI ;
      bf:identifiedBy ?ownIDEntityURI .

    ?countryEntityURI a schema:PostalAddress ;
                      rdfs:comment "Created from BnF data" ;
                      schema:addressCountry ?country .

    ?authorityURIISNI a bf:Isni ;
                      rdfs:comment "Created from BnF data" ;
                      rdf:value ?bnfAuthorityISNI .

    ?viafEntityURI a bf:Identifier ;
                   rdfs:comment "Created from BnF data" ;
                   rdfs:label "VIAF" ;
                   rdf:value ?viaf .

    ?wdEntityURI a bf:Identifier ;
                 rdfs:comment "Created from BnF data" ;
                 rdfs:label "Wikidata" ;
                 rdf:value ?wd .

    ?ownIDEntityURI a bf:Identifier ;
                    rdfs:comment "Created from BnF data" ;
                    rdfs:label "BnF" ;
                    rdf:value ?ownID .

  }
}
WHERE {

    graph <http://bnf-contributors> { ?bnfAuthority a foaf:Organization . }   

    #
    # Get some optional properties
    #
    OPTIONAL { graph <http://bnf-contributors> { ?bnfAuthority foaf:name ?bnfAuthorityLabel . } }

    BIND( STRUUID() as ?uuid)
    BIND( IRI( CONCAT( "http://kbr.be/id/data/", ?uuid ) ) as ?authorityURI)

    # Get optional own ID
    OPTIONAL {
      graph <http://bnf-contributors> { ?bnfAuthority dcterms:identifier ?ownID . } 
      BIND(iri(concat("http://kbr.be/id/data/identifier_bnf_", ?ownID)) as ?ownIDEntityURI)
    }

    # get optional location
    OPTIONAL {
      graph <http://bnf-contributors> {
        ?bnfAuthority rdagroup2elements:placeAssociatedWithTheCorporateBody ?country .
      }
    }
    BIND(iri(concat("http://kbr.be/id/data/location_", ?uuid)) as ?countryEntityURI)

    #
    # Get optional VIAF
    #
    OPTIONAL {
      graph <http://bnf-contributors> {
        ?bnfAuthority bf:identifiedBy ?viafEntity .

        ?viafEntity a bf:Identifier ;
                    rdfs:label "VIAF" ;
                    rdf:value ?viaf .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_viaf_", ?viaf)) as ?viafEntityURI)
    }

    #
    # Get optional WIKIDATA
    #
    OPTIONAL {
      graph <http://bnf-contributors> {
        ?bnfAuthority bf:identifiedBy ?wdEntity .

        ?wdEntity a bf:Identifier ;
                  rdfs:label "Wikidata" ;
                  rdf:value ?wd .
      }
      BIND(iri(concat("http://kbr.be/id/data/identifier_wikidata_", ?wd)) as ?wdEntityURI)
    }


    #
    # Get the linked ISNI identifier
    #
    OPTIONAL {
      graph <http://bnf-contributors> { 
        ?bnfAuthority bf:identifiedBy ?isniAuthorEntity . 
        ?isniAuthorEntity a bf:Isni ;
                    rdf:value ?bnfAuthorityISNI . 
      }
      BIND( IRI( CONCAT( "http://kbr.be/id/data/identifier_isni_", ?bnfAuthorityISNI ) ) as ?authorityURIISNI)
    }         

    #
    # Only create a new authority if there isn't already an authority with the given ISNI
    #
    FILTER NOT EXISTS {
      graph <http://beltrans-contributors> {
        ?p a schema:Organization ;
           schema:sameAs ?bnfAuthority .
      }
    }
}
