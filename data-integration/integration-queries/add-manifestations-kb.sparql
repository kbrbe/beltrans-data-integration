prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix lang: <http://id.loc.gov/vocabulary/languages/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX bnf-onto: <http://data.bnf.fr/ontology/bnf-onto/>
PREFIX frbr: <http://rdvocab.info/uri/schema/FRBRentitiesRDA/>
PREFIX rda-wemi: <http://rdvocab.info/RDARelationshipsWEMI/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/>
PREFIX rdagroup1elements: <http://rdvocab.info/Elements/>


INSERT {
  graph <http://beltrans-manifestations> {
    ?manifestationURI a schema:CreativeWork ;
      rdfs:label ?manifestationLabel ;
      dcterms:identifier ?uuid ;
      schema:name ?manifestationTitle ;
      schema:inLanguage ?targetLang ;
      btm:sourceLanguage ?sourceLang ;
      schema:sameAs ?kbManifestation ;
      rdfs:comment "Created from KB data" ;
      bibo:isbn10 ?isbn10 ;
      bibo:isbn13 ?isbn13 .
  }
}
WHERE {

    graph <http://kb-publications> { 
      ?kbManifestation a schema:CreativeWork ;
                        dcterms:identifier ?manifestationID ;
                        schema:name ?manifestationTitle .
    }

    OPTIONAL { graph <http://kb-publications> { ?kbManifestation schema:inLanguage ?targetLang . } }
    OPTIONAL { 
      graph <http://kb-publications> { ?kbManifestation schema:translationOfWork ?kbOriginal . }
      graph <http://kb-originals> { ?kbOriginal schema:inLanguage ?sourceLang . } 
    }

    BIND( STRUUID() as ?uuid)
    BIND( IRI( CONCAT( "http://kbr.be/id/data/", ?uuid ) ) as ?manifestationURI)
    BIND( CONCAT( "BELTRANS manifestation ", ?uuid ) as ?manifestationLabel)

    #
    # Get optional ISBN10
    #
    OPTIONAL { graph <http://kb-publications> { ?kbManifestation bibo:isbn10 ?isbn10 . } }

    #
    # Get optional ISBN13
    #
    OPTIONAL { graph <http://kb-publications> { ?kbManifestation bibo:isbn13 ?isbn13 . } }

    #
    # Only create a new authority if there isn't already an authority with the given ISNI
    #
    FILTER NOT EXISTS {
      graph <http://beltrans-manifestations> {
        ?p a schema:CreativeWork ;
           schema:sameAs ?kbManifestation .
      }
    }
}
