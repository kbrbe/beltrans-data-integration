prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix prov: <http://www.w3.org/ns/prov#>
prefix bio: <http://vocab.org/bio/0.1/>
prefix dc: <http://purl.org/dc/elements/1.1/>
prefix dcterms: <http://purl.org/dc/terms/>
prefix schema: <http://schema.org/>
prefix bibo: <http://purl.org/ontology/bibo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX btm: <http://kbr.be/ns/beltrans/model#>
PREFIX btid: <http://kbr.be/id/data/> 
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX btisni: <http://kbr.be/isni/>
PREFIX mads: <http://www.loc.gov/mads/rdf/v1#>
PREFIX up: <http://users.ugent.be/~tdenies/up/>
PREFIX marcrel: <http://id.loc.gov/vocabulary/relators/> 

SELECT DISTINCT
  ?contributorID
  (group_concat(distinct ?contributorLabel;SEPARATOR=';') AS ?name)
  #(group_concat(distinct ?contributorType;SEPARATOR=';') as ?type)
  (group_concat(distinct ?nameVariantNameID;SEPARATOR=';') as ?names)
  (group_concat(distinct ?pseudonymNameID;SEPARATOR=';') as ?textPseudonymOf)
  (group_concat(distinct ?otherIdentityNameID;SEPARATOR=';') as ?textRealNameOf)
  (group_concat(distinct ?pseudonymIdentifier;SEPARATOR=';') as ?pseudonymOf)
  (group_concat(distinct ?otherIdentityIdentifier;SEPARATOR=';') as ?realNameOf)
  (group_concat(distinct ?nationalityLabel;SEPARATOR=';') AS ?nationalities)
  (group_concat(distinct ?nationality;SEPARATOR=';') AS ?nationalityCountryCodes)
  (group_concat(distinct ?languageLabel;SEPARATOR=';') AS ?languages)
  (group_concat(distinct ?language;SEPARATOR=';') AS ?languagesCodes)
  (group_concat(distinct ?genderLabel;SEPARATOR=';') AS ?gender)
  (group_concat(distinct ?kbrBirthDate;SEPARATOR=';') AS ?birthDateKBR)
  (group_concat(distinct ?bnfBirthDate;SEPARATOR=';') AS ?birthDateBnF)
  (group_concat(distinct ?ntaBirthDate;SEPARATOR=';') AS ?birthDateNTA)
  (group_concat(distinct ?isniBirthDate;SEPARATOR=';') AS ?birthDateISNI)
  (group_concat(distinct ?kbrDeathDate;SEPARATOR=';') AS ?deathDateKBR)
  (group_concat(distinct ?bnfDeathDate;SEPARATOR=';') AS ?deathDateBnF)
  (group_concat(distinct ?ntaDeathDate;SEPARATOR=';') AS ?deathDateNTA)
  (group_concat(distinct ?isniDeathDate;SEPARATOR=';') AS ?deathDateISNI)
  (group_concat(distinct ?kbrID;SEPARATOR=';') AS ?kbrIDs)
  (group_concat(distinct ?bnfID;SEPARATOR=';') AS ?bnfIDs)
  (group_concat(distinct ?ntaID;SEPARATOR=';') AS ?ntaIDs)
  (group_concat(distinct ?unescoID;SEPARATOR=';') AS ?unescoIDs)
  (group_concat(distinct ?isni;SEPARATOR=';') AS ?isniIDs)
  (group_concat(distinct ?viaf;SEPARATOR=';') AS ?viafIDs)
  (group_concat(distinct ?wikidata;SEPARATOR=';') AS ?wikidataIDs)
WHERE {

  graph <http://beltrans-manifestations> { ?manifestation schema:author|schema:translator|marcrel:ill|marcrel:sce|marcrel:pbd ?contributorURI . }

  graph <http://beltrans-contributors> {
    ?contributorURI a schema:Person ;
                    dcterms:identifier ?contributorID .
  }

  OPTIONAL { graph <http://beltrans-contributors> { ?contributorURI schema:name ?contributorLabel . } }

  # NATIONALITY
  #
  OPTIONAL {
    graph <http://beltrans-contributors> { ?contributorURI schema:nationality ?nationality . } 
    graph <http://master-data> { ?nationality mads:authoritativeLabel ?nationalityLabel . }
    FILTER (lang(?nationalityLabel) = 'en')
  }

  # LANGUAGE
  #
  OPTIONAL {
    graph <http://beltrans-contributors> { ?contributorURI schema:knowsLanguage ?language . } 
    graph <http://master-data> { ?language mads:authoritativeLabel ?languageLabel . }
    FILTER (lang(?languageLabel) = 'en')
  }



  #
  # GENDER
  #
  OPTIONAL {
    graph <http://beltrans-contributors> { ?contributorURI schema:gender ?gender . } 
    graph <http://master-data> { ?gender rdfs:label ?genderLabel . }
    FILTER (lang(?genderLabel) = 'en')
  }
  
  # ISNI
  #
  OPTIONAL {
    graph <http://beltrans-contributors> {
      ?contributorURI bf:identifiedBy ?isniEntity .
      ?isniEntity a bf:Isni ;
                  rdf:value ?isni .
    } 

    BIND( IRI(CONCAT("https://isni.org/isni/", str(?isni))) as ?isniURI)
    OPTIONAL { graph <http://isni-rdf> { ?isniURI schema:birthDate ?isniBirthDate . } }
    OPTIONAL { graph <http://isni-rdf> { ?isniURI schema:deathDate ?isniDeathDate . } }
  }

  # VIAF
  #
  OPTIONAL {
    graph <http://beltrans-contributors> {
      ?contributorURI bf:identifiedBy ?viafEntity .
      ?viafEntity a bf:Identifier ;
                  rdfs:label "VIAF" ;
                  rdf:value ?viaf .
    } 
  }

  # Wikidata
  #
  OPTIONAL {
    graph <http://beltrans-contributors> {
      ?contributorURI bf:identifiedBy ?wdEntity .
      ?wdEntity a bf:Identifier ;
                rdfs:label "Wikidata" ;
                rdf:value ?wikidata .
    } 
  }


  # SOURCE DATA: KBR
  #
  OPTIONAL {
    graph <http://beltrans-contributors> { ?contributorURI schema:sameAs ?kbrURI . } 
    graph <http://kbr-linked-authorities> {
      ?kbrURI a schema:Person ;
              dcterms:identifier ?kbrID .
    }


    OPTIONAL { graph <http://kbr-linked-authorities> { ?kbrURI schema:birthDate ?kbrBirthDate . } }
    OPTIONAL { graph <http://kbr-linked-authorities> { ?kbrURI schema:deathDate ?kbrDeathDate . } }

    # get other name variants of this record
    #
    OPTIONAL {
      graph <http://kbr-linked-authorities> { 
        ?kbrURI btm:hasNameVariant ?nameVariantURI .

        ?nameVariantURI rdfs:label ?nameVariantLabel ;
                        dcterms:identifier ?nameVariantIdentifier .
      }
      BIND( COALESCE(?nameVariantIdentifier, "") AS ?nameVariantIDString)
      BIND( CONCAT( COALESCE(?nameVariantLabel, "missing name"),
                    IF(str(?nameVariantIDString) = "", "",
                       CONCAT(" (", str(?nameVariantIDString), ")")) )
            AS ?nameVariantNameID
          )
    }

    # get pseudonyms of this record
    #
    OPTIONAL {
      graph <http://kbr-linked-authorities> { 
        ?kbrURI btm:hasPseudonym ?pseudonymURI .

        ?pseudonymURI rdfs:label ?pseudonymLabel ;
                      dcterms:identifier ?pseudonymIdentifier .
      }
      BIND( COALESCE(?pseudonymIdentifier, "") AS ?pseudonymIDString)
      BIND( CONCAT( COALESCE(?pseudonymLabel, "missing name"),
                    IF(str(?pseudonymIDString) = "", "",
                       CONCAT(" (", str(?pseudonymIDString), ")")) )
            AS ?pseudonymNameID
          )

    }

    # if this is a pseudonym, get also other pseudonyms or the real identity
    #
    OPTIONAL {
      graph <http://kbr-linked-authorities> { 
        ?kbrURI btm:isPseudonymOf ?otherIdentityURI .

        ?otherIdentityURI rdfs:label ?otherIdentityLabel ;
                          dcterms:identifier ?otherIdentityIdentifier .
      }
      BIND( COALESCE(?otherIdentityIdentifier, "") AS ?otherIdentityIDString)
      BIND( CONCAT( COALESCE(?otherIdentityLabel, "missing name"),
                    IF(str(?otherIdentityIDString) = "", "",
                       CONCAT(" (", str(?otherIdentityIDString), ")")) )
            AS ?otherIdentityNameID
          )

    }


  }

  # SOURCE DATA: BnF
  #
  OPTIONAL {
    graph <http://beltrans-contributors> { ?contributorURI schema:sameAs ?bnfURI . } 
    graph <http://bnf-contributors> {
      ?bnfURI a foaf:Person ;
              dcterms:identifier ?bnfID .
    }

    OPTIONAL { graph <http://bnf-contributors> { ?bnfURI bio:birth ?bnfBirthDate . } }
    OPTIONAL { graph <http://bnf-contributors> { ?bnfURI bio:death ?bnfDeathDate . } }
  }

  # SOURCE DATA: NTA
  #
  OPTIONAL {
    graph <http://beltrans-contributors> { ?contributorURI schema:sameAs ?ntaURI . } 
    graph <http://kb-linked-authorities> {
      ?ntaURI a schema:Person ;
              dcterms:identifier ?ntaID .
    }

    OPTIONAL { graph <http://kbr-linked-authorities> { ?ntaURI schema:birthDate ?ntaBirthDate . } }
    OPTIONAL { graph <http://kbr-linked-authorities> { ?ntaURI schema:deathDate ?ntaDeathDate . } }
  }

  # SOURCE DATA: UNESCO
  #
  OPTIONAL {
    graph <http://beltrans-contributors> { ?contributorURI schema:sameAs ?unescoURI . } 
    graph <http://unesco-linked-authorities> {
      ?unescoURI a schema:Person ;
                 dcterms:identifier ?unescoID .

      ?unescoURI bf:identifiedBy ?unescoLongEntity .

      ?unescoLongEntity a bf:Identifier ;
                        rdfs:label "UnescoLong" ;
                        rdf:value ?unescoIDLong .
    }

  }



}
GROUP BY ?contributorID
